import me.rancraftplayz.pacifist.remapper.RemapperPlugin

buildscript {
    dependencies{
        classpath files('libs/pacifist-remapper-0.1-all.jar')
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.5.0'
    id 'net.kyori.indra' version "2.0.6"
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

apply plugin: RemapperPlugin

group 'me.rancraftplayz'
version '1.0-SNAPSHOT'

targetCompatibility = JavaVersion.VERSION_16
sourceCompatibility = JavaVersion.VERSION_16

repositories {
    mavenCentral()
    maven { url = 'https://repo.spongepowered.org/maven' }
    maven { url = "https://maven.quiltmc.org/repository/release/" }
    maven { url = "https://maven.elmakers.com/repository/" }
    maven { url = "https://repo.sk1er.club/repository/maven-public" }
    maven { url = "https://repo.sk1er.club/repository/maven-releases/" }
    mavenLocal()
}

configurations {
    shadowMe
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    shadowMe "org.jetbrains.kotlin:kotlin-stdlib"

    // Spigot
    compileOnly 'org.spigotmc:spigot:1.17.1-R0.1-SNAPSHOT:remapped-mojang'
    compileOnly 'org.spigotmc:spigot:1.17.1-R0.1-SNAPSHOT'

    // Spigot but better
    compileOnly fileTree(dir: 'libs', include: 'hax.jar')

    // Remapper
    mojangProguardMappings "org.spigotmc:minecraft-server:1.17.1-R0.1-SNAPSHOT:maps-mojang@txt"
    spigotCsrgMappings "org.spigotmc:minecraft-server:1.17.1-R0.1-SNAPSHOT:maps-spigot@csrg"
    remapLib "org.spigotmc:spigot:1.17.1-R0.1-SNAPSHOT:remapped-mojang"

    accessWidener fileTree(dir: 'src/main/resources', include: ['*.accesswidener'])
    accessWidenerLib "org.spigotmc:spigot:1.17.1-R0.1-SNAPSHOT:remapped-mojang"

    // Ignite
    implementation "space.vectrix.ignite:ignite-api:0.5.1"

    // Mixins
    compileOnly "org.spongepowered:mixin:0.8.3"

    // Config Manager because sponge didn't like me very much (and this is the only config manager that i found other than sponge)
    implementation("gg.essential:vigilance-1.17-fabric:166+next") {
        exclude module: 'fabric-loader'
    }
    shadowMe("gg.essential:vigilance-1.17-fabric:166+next") {
        exclude module: 'fabric-loader'
    }

    // Utils
    implementation "it.unimi.dsi:fastutil:8.5.4"
    shadowMe "it.unimi.dsi:fastutil:8.5.4"
    implementation fileTree(dir: 'libs', include: 'JavaUtils.jar')
    shadowMe fileTree(dir: 'libs', include: 'JavaUtils.jar')
    implementation("gg.essential:essential-1.8.9-forge:1438+release-launch") {
        exclude group: "*"
    }
    shadowMe("gg.essential:essential-1.8.9-forge:1438+release-launch") {
        exclude group: "*"
    }
}

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    configurations = [project.configurations.getByName("shadowMe")]
    archiveBaseName.set("${project.name}-${project.version}")
    archiveClassifier.set('')
    archiveVersion.set('')

    exclude(
            "**/LICENSE.md",
            "**/LICENSE.txt",
            "**/LICENSE",
            "**/NOTICE",
            "**/NOTICE.txt",
            "pack.mcmeta",
            "dummyThing",
            "**/module-info.class",
            "META-INF/proguard/**",
            "META-INF/maven/**",
            "META-INF/versions/**",
            "META-INF/com.android.tools/**",
            "fabric.mod.json"
    )
    mergeServiceFiles()
}
tasks.getByName("remapJar").dependsOn(tasks.getByName("shadowJar"))

compileJava {
    options.release = 16
}

/**
 * To build the jar you need to run the task remapJar
 */